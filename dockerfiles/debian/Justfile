set unstable := true

import '../../lib/podman.just'
import '../vars.just'

self := just_executable() + ' -f ' + source_file()

[script('bash')]
build *archs:
  archs='{{ if archs == '' { default_archs } else { archs } }}'
  for arch in $archs ; do
    {{ self }} build-debian $arch || exit 1
  done

[script('bash')]
clean version: \
  (podman_manifest_clean debian_image + ":" + version)

[private]
build-debian arch: \
  (podman_manifest debian_image_full)
  podman build --platform linux/{{ arch }} \
    --manifest="{{ debian_image_full }}" \
    --build-arg VERSION="{{ debian_version }}" .
