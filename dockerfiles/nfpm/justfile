set unstable := true

import '../../lib/lib.just'
import '../../lib/podman.just'
import '../vars.just'

self := just_executable() + ' -f ' + source_file()
nfpm_download_url := 'https://github.com/goreleaser/nfpm/releases/download'

[script('bash')]
build *archs:
  archs='{{ if archs == '' { default_archs } else { archs } }}'
  for arch in $archs ; do
    {{ self }} build-nfpm $arch || exit 1
  done


[private, script('bash')]
download-nfpm version arch:
  source "{{ builtin_bash_functions }}"
  arch=$(go-arch "{{ arch }}")

  name=nfpm_{{ version }}_${arch}
  curl -L -o ${name}.deb -C - {{ nfpm_download_url }}/v{{ version }}/${name}.deb

[private, script('bash')]
build-nfpm arch: \
  (download-nfpm nfpm_version arch) \
  (podman_manifest nfpm_image_full builder_source builder_licenses)
  source "{{ builtin_bash_functions }}"
  arch=$(go-arch "{{ arch }}")

  name="nfpm_{{ nfpm_version }}_${arch}"
  podman build --platform linux/{{ arch }} \
    --manifest="{{ nfpm_image_full }}" \
    --build-arg BASE_IMAGE={{ debian_image_full }} \
    --build-arg PKG_NAME="${name}" .
