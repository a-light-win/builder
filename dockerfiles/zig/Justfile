set unstable := true

self := just_executable() + ' -f ' + source_file()

import '../../lib/lib.just'
import '../../lib/podman.just'
import '../vars.just'

# build a multi-arch image for zig
[script('bash')]
build *archs:
  archs='{{ if archs == '' { default_archs } else { archs } }}'
  for arch in $archs ; do
    {{ self }} build-zig $arch || exit 1
  done

# clean up docker image of zig by version
[script('bash')]
clean version: \
  (podman_manifest_clean zig_image + ':' + version)

[private, script('bash')]
download version arch platform:
  name=zig-{{ platform }}-{{ arch }}-{{ version }}

  if [ -e "${name}" ] ; then
    echo "The ${name} already downloaded, skipping"
    exit 0
  fi

  curl -L -o ${name}.tar.xz https://ziglang.org/download/{{ version }}/${name}.tar.xz || exit 1
  tar xf ${name}.tar.xz

[private, script('bash')]
build-zig arch platform='linux': \
  (download zig_version arch platform) \
  (podman_manifest zig_image_full builder_source builder_licenses)
  podman build --platform {{ platform }}/{{ arch }} \
    --manifest="{{ zig_image_full }}" \
    --build-arg BASE_IMAGE={{ debian_image_full }} \
    --build-arg ARCH={{ arch }} \
    --build-arg VERSION={{ zig_version }} .
