set unstable := true
set fallback

self := just_executable() + ' -f ' + source_file()

import '../../lib/podman.just'
import '../../lib/exec_alias.just'
import '../vars.just'

# build a multi-arch image for zig
[script('bash')]
build version *archs:
  archs='{{ if archs == '' { default_archs } else { archs } }}'
  for arch in $archs ; do
    {{ self }} build-zig {{ version }} $arch || exit 1
  done

# clean up docker image of zig by version
[script('bash')]
clean version: \
  (podman_manifest_clean base_image_path / 'zig:' + version)

[private, script('bash')]
download version arch platform:
  name=zig-{{ platform }}-{{ arch }}-{{ version }}

  if [ -e "${name}" ] ; then
    echo "The ${name} already downloaded, skipping"
    exit 0
  fi

  curl -L -o ${name}.tar.xz https://ziglang.org/download/{{ version }}/${name}.tar.xz || exit 1
  tar xf ${name}.tar.xz

[private, script('bash')]
build-zig version arch platform='linux': \
  (download version arch platform) \
  (podman_manifest base_image_path / 'zig:' + version)
  podman build --platform {{ platform }}/{{ arch }} \
    --manifest="{{ base_image_path / 'zig:' + version }}" \
    --build-arg BASE_IMAGE_PATH={{ base_image_path }} \
    --build-arg ARCH={{ arch }} \
    --build-arg VERSION={{ version }} .
