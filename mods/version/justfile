set unstable := true

import '../../lib/lib.just'

[no-cd, script('bash')]
bump version module='': is-clean && (commit version module) (tag version module)
  source "{{ builtin_bash_functions }}"
  version-bump "{{ version }}" "{{ module }}"

[private, no-cd, script('bash')]
is-clean:
  # check if the working directory is clean
  git diff --quiet && git diff --cached --quiet

[private, no-cd, script('bash')]
commit version module='':
  module={{ if module == '' {''} else { module + ' ' } }}
  git add -u
  git commit -m "Bump ${module}version to {{ version }}"

[private, no-cd, script('bash')]
tag version module='':
  version={{ if module == '' { version } else { module + '-' + version } }}
  git tag "${version}" -m "Bump ${version}"
