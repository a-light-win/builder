set unstable := true

import '../../lib/lib.just'

[no-cd, script('bash')]
setup:
  source "{{ builtin_bash_functions }}"
  podman-setup
  setup-qemu-user-static

[no-cd, script('bash')]
build arch='x86_64': (pre-build-image arch) create-manifest
  source "{{ builtin_bash_functions }}"
  arch=$(build-arch "{{ arch }}")
  load-builder-env

  podman-build "${arch}"

[no-cd, script('bash')]
pack-all: (build 'x86_64') (build 'aarch64')

[no-cd, script('bash')]
clean version="":
  source "{{ builtin_bash_functions }}"
  load-builder-env

  export PKG_VERSION="{{ if version != '' { version } else { `$PKG_VERSION` } }}"
  podman-manifest-clean

[no-cd, private, script('bash')]
create-manifest:
  source "{{ builtin_bash_functions }}"
  load-builder-env

  podman-manifest
  
[no-cd, private, script('bash')]
pre-build-image arch:
  source "{{ builtin_bash_functions }}"
  arch=$(build-arch "{{ arch }}")
  load-builder-env

  pre-build-image "${arch}"

[no-cd, script('bash')]
upload:
  source "{{ builtin_bash_functions }}"
  load-builder-env

  podman-tag-and-push
