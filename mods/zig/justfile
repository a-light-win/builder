set unstable := true

import '../../lib/lib.just'
import '../../dockerfiles/vars.just'

pkg_target := env('PKG_TARGET', 'zig-out')

# Build a zig program
[no-cd, script('bash'), positional-arguments]
build target release='safe' *args='':
  source "{{ builtin_bash_functions }}"
  # target: one of x86_64-linux-musl, aarch64-linux-musl
  # realease: one of safe, fast and small
  podman-zig {{ zig_image_full }} build \
    --release="{{ release }}" \
    -Dtarget="{{ target }}" {{ if args != '' { "${@:3}" } else { '' } }}

[no-cd, script('bash')]
test:
  source "{{ builtin_bash_functions }}"
  podman-zig {{ zig_image_full }} build test

[no-cd, script('bash')]
clean:
  source "{{ builtin_bash_functions }}"
  zig-clean
