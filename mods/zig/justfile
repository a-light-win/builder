set unstable := true

import '../../lib/lib.just'

# Build a zig program with specified target
[no-cd, script('bash')]
build arch='x86_64':
  source "{{ builtin_bash_functions }}"
  zig-build "{{ arch }}"

[no-cd, script('bash')]
test:
  source "{{ builtin_bash_functions }}"
  podman-zig build test

[no-cd, script('bash')]
cov:
  # TODO: implement coverage
  echo "Coverage is not implemented yet"

[no-cd, script('bash')]
clean: nfpm-clean
  source "{{ builtin_bash_functions }}"
  zig-clean

# build and package on all supported architectures
# with all supported packaging formats
[no-cd]
pack-all: pack-x86_64 pack-aarch64

[no-cd]
pack-x86_64: (build 'x86_64') (nfpm-pack 'x86_64')
[no-cd]
pack-aarch64: (build 'aarch64') (nfpm-pack 'aarch64')

[no-cd]
upload repo: (nfpm-upload-packages repo)
